OUTPUT_FORMAT("elf64-x86-64")
OUTPUT_ARCH(i386:x86-64)
ENTRY(start)
SECTIONS
{
   /*kernelStart = 0x40000000; */
   kernelStart = 0xffffffff80000000;   
 . = 0x100000;
 .text.head : AT(ADDR(.text.head)   ){
       arch/x86_64/boot.o(.text)
       arch/x86_64/smp/head.o(.text)
       arch/x86_64/smp/trampoline_64.o(.text)
 }
 .confdata.head :AT(ADDR(.confdata.head)  ) {
       arch/x86_64/boot.o(.confdata)
       arch/x86_64/smp/head.o(.confdata)
       arch/x86_64/smp/trampoline_64.o(.confdata)
  }
 .data.head :AT(ADDR(.data.head)  ) {
       arch/x86_64/boot.o(.data)
       arch/x86_64/smp/head.o(.data)
       arch/x86_64/smp/trampoline_64.o(.data)
  }
 .bss.head : AT(ADDR(.bss.head)   ) {
       arch/x86_64/boot.o(.bss)
       arch/x86_64/smp/head.o(.bss)
       arch/x86_64/smp/trampoline_64.o(.bss)
 }

 . = . + kernelStart;  
 . = ALIGN(4096 ); 
 .text : AT(ADDR(.text) - kernelStart ) {
        __text_start = . ;
        *(EXCLUDE_FILE (*arch/x86_64/boot.o *arch/x86_64/smp/head.o *arch/x86_64/smp/trampoline_64.o)
          .text)
        __text_end = .;
 }
 
 . = kernelStart + 0x200000;
 __data_start = .;   
 confdata : AT(ADDR(confdata) - kernelStart ){
 	*(confdata)
	
 }
 __confdata_end = .;
  
 .data : AT(ADDR(.data) - kernelStart ){
 
        *(EXCLUDE_FILE (*arch/x86_64/boot.o *arch/x86_64/smp/head.o *arch/x86_64/smp/trampoline_64.o)
          .data)
        *(.rodata)
        __data_end = .;
   }
   
 _edata=.;
 __bss_start = .;
  
 .bss : AT(ADDR(.bss) - kernelStart ){
        *(EXCLUDE_FILE (*arch/x86_64/boot.o *arch/x86_64/smp/head.o *arch/x86_64/smp/trampoline_64.o)
          .bss)
        *(EXCLUDE_FILE (*arch/x86_64/boot.o *arch/x86_64/smp/head.o *arch/x86_64/smp/trampoline_64.o)
          COMMON)
 }
 _end = .;
 end = .;
 __kernel_end = .;
 
 
 /DISCARD/ : {
  *(.eh_frame)
  *(.comment)
  *(.note)
 }
}
